#include <Arduino.h>
#include <EEPROM.h>
#include <Ethernet.h>
#include <SPI.h>
#include "pico/stdlib.h"
#include "hardware/timer.h"
#include <EthernetUdp.h>
//--------------------------------------------------
// ê³µìš© ì •ì˜
//--------------------------------------------------
#define W5500_CS 17       // Ethernetìš© W5500 Chip Select í•€
#define EEPROM_SIZE 512   // EEPROM(Flash) ë©”ëª¨ë¦¬ í¬ê¸°

//--------------------------------------------------
// [1] DAQë³´ë“œì™€ì˜ Serial1 Modbus í†µì‹  (ì„¼ì„œ ë°ì´í„° ìˆ˜ì§‘)
//--------------------------------------------------
constexpr unsigned char SLAVE_ADDRESS      = 0x01;
constexpr unsigned char FUNCTION_CODE      = 0x03;
constexpr uint16_t      REGISTER_START     = 0x0100;
constexpr unsigned char REGISTER_COUNT     = 4;
constexpr unsigned char RESPONSE_MAX_LEN   = 32;
char receivedDataByte = 0;


// í‰ê·  ë‚´ê¸° ìœ„í•´ ì‚¬ìš©í–ˆë˜ ë°ì´í„° ìŠ¤í† ë¦¬ì§€
// unsigned char dataStorage[MAX_STORAGE_SIZE][255];
// unsigned char avgStorage[MAX_AVG_STORAGE_SIZE][RECEIVED_DATA_BYTE];
// int avgIndex  = 0;  // 5ë¶„ í‰ê·  ì €ì¥ ê°œìˆ˜
//int dataIndex = 0;  // í˜„ì¬ ë°ì´í„° ê°œìˆ˜
//constexpr int MAX_STORAGE_SIZE = 600;   // 5ë¶„ ì €ì¥ (0.5ì´ˆ ê°„ê²© x 600)
// constexpr int MAX_AVG_STORAGE_SIZE = 6;   // 30ë¶„ ì €ì¥ (5ë¶„ í‰ê·  x 6)

// ìµœì‹  ì„¼ì„œ ë°ì´í„° (Modbus ì‘ë‹µì—ì„œ ë°›ì€ 8ë°”ì´íŠ¸)
unsigned char latestSensorData[255] = {0};

volatile bool requestFlag = false; // DAQ ìš”ì²­ flag
volatile bool avgCalcFlag = false;   // í‰ê·  ê³„ì‚° flag

repeating_timer_t modbusTimer;    // 0.5ì´ˆë§ˆë‹¤ ì‹¤í–‰
// repeating_timer_t modbusAvgTimer; // 5ë¶„ë§ˆë‹¤ ì‹¤í–‰

//--------------------------------------------------
// [2] PCì™€ì˜ Serial í†µì‹  (ë„¤íŠ¸ì›Œí¬/RS485 ì„¤ì • via EEPROM)
//--------------------------------------------------
struct Settings {
  // ë„¤íŠ¸ì›Œí¬ ì„¤ì •
  char mac[18];      // ì˜ˆ: "DE:AD:BE:EF:FE:ED"
  char ip[16];       // ì˜ˆ: "192.168.1.100"
  char subnet[16];   // ì˜ˆ: "255.255.255.0"
  char gateway[16];  // ì˜ˆ: "192.168.1.1"
  char dns[16];      // ì˜ˆ: "8.8.8.8"
  char serverIp[16]; // ì„œë²„ IP (í•„ìš” ì‹œ)
  char protocol[4];  // í”„ë¡œí† ì½œ ë¬¸ìì—´
  int port;          // Ethernet ì„œë²„ í¬íŠ¸
  
  // RS-485 ì„¤ì •
  uint8_t slaveAddress;   // êµ­ë²ˆ (1~255)
  uint16_t readAddress;   // ì½ê¸° ì£¼ì†Œ
  uint16_t numOfBlocks;   // ì½ì„ ë¸”ë¡ ê°œìˆ˜ (1~125)
};

Settings deviceSettings;  // ì „ì—­ ì„¤ì • êµ¬ì¡°ì²´

//--------------------------------------------------
// [3] Ethernet ModbusTCP ì„œë²„ (ì„¼ì„œ ë°ì´í„° ì „ì†¡)
//--------------------------------------------------
// Ethernet ì„œë²„ ê°ì²´ (ë™ì  í• ë‹¹í•˜ì—¬ ë„¤íŠ¸ì›Œí¬ ì„¤ì • ë³€ê²½ ì‹œ ì¬ìƒì„±)
EthernetServer* modbusTCPServer = nullptr;
EthernetUDP udp;
bool udpActive = false; //UDPì—´ë ¤ìˆëŠ”ì§€ í™•ì¸ìš© flag
//--------------------------------------------------
// í•¨ìˆ˜ í”„ë¡œí† íƒ€ì…
//--------------------------------------------------
// [1] DAQë³´ë“œ ê´€ë ¨
bool requestModbusData(repeating_timer_t *t);
bool calculateFiveMinAverageTimer(repeating_timer_t *t);
void sendModbusRequest();
void receiveModbusResponse();
bool validateModbusResponse(unsigned char *response, int length);
uint16_t calculateCRC(unsigned char *data, unsigned char length);
// void calculateFiveMinAverage();
// void calculateThirtyMinAverage();
void clearSerial1Buffer();

// [2] Serial(Ethernet ì„¤ì •) ê´€ë ¨
void loadSettingsFromEEPROM();
void saveSettingsToEEPROM();
void parseAndSaveSettings(String command);
void sendSettings();
void applyNetworkSettings();

// [3] Ethernet ModbusTCP ê´€ë ¨
void sendModbusTCPResponse(EthernetClient &client, byte *request);
bool validateModbusTCPRequest(byte *request, int len);
void sendUDPSensorData();

//--------------------------------------------------
// setup() ë° loop()
//--------------------------------------------------
void setup() 
{
    Serial.begin(115200);
    Serial1.begin(9600, SERIAL_8N1);
    
    // EEPROM ì´ˆê¸°í™” ë° ë„¤íŠ¸ì›Œí¬ ì„¤ì • ë¶ˆëŸ¬ì˜¤ê¸°
    EEPROM.begin(EEPROM_SIZE);
    Serial.println("EEPROM ì´ˆê¸°í™” ì™„ë£Œ, ë°ì´í„° ë¶ˆëŸ¬ì˜¤ëŠ” ì¤‘...");
    loadSettingsFromEEPROM();
    applyNetworkSettings();

    // Ethernet ì„œë²„ ìƒì„± (deviceSettings.portê°€ 0ì´ë©´ ê¸°ë³¸ 503 ì‚¬ìš©)
    startModbusServer();

    // DAQ ë³´ë“œ í†µì‹  íƒ€ì´ë¨¸ ì‹œì‘
    add_repeating_timer_ms(500, requestModbusData, NULL, &modbusTimer);
    //   add_repeating_timer_ms(300000, calculateFiveMinAverageTimer, NULL, &modbusAvgTimer);
}

void loop() 
{
    // [1] DAQ ë³´ë“œì™€ì˜ í†µì‹  ì²˜ë¦¬
    if (requestFlag) 
    {
        requestFlag = false;
        sendModbusRequest();
        receiveModbusResponse();
    }
    
    // [2] PCì™€ì˜ Serial ëª…ë ¹ ì²˜ë¦¬ (ë„¤íŠ¸ì›Œí¬/RS485 ì„¤ì •)
    if (Serial.available()) 
    {
        String command = Serial.readStringUntil('\n');
        command.trim();
        if (command.startsWith("WRITE")) 
        {
            parseAndSaveSettings(command);
            applyNetworkSettings();
            // Ethernet ì„œë²„ë„ ìƒˆ ë„¤íŠ¸ì›Œí¬ ì„¤ì •ì— ë§ê²Œ ì¬ì‹œì‘
            startModbusServer();
        } 
        else if (command.startsWith("READ_SETTINGS")) 
        {
            sendSettings();
        }
    }
  
    // [3] Ethernet ModbusTCP í´ë¼ì´ì–¸íŠ¸ ìš”ì²­ ì²˜ë¦¬
    // UDP ëª¨ë“œì—ì„œëŠ” ì„¼ì„œ ë°ì´í„°ê°€ ìˆ˜ì‹ ë  ë•Œë§ˆë‹¤ deviceSettings.serverIpë¡œ í‘¸ì‹œë©ë‹ˆë‹¤.
    if (strcmp(deviceSettings.protocol, "TCP") == 0) 
    {
        EthernetClient client = modbusTCPServer->available();
        if (client) 
        {
            Serial.println("Client Connected!");
            byte request[12];
            int index = 0;
            unsigned long startTime = millis();
            // 0.5ì´ˆ ì´ë‚´ì— ìš”ì²­ ë°ì´í„°ë¥¼ ìˆ˜ì‹ 
            while (millis() - startTime < 500) 
            {
                if (client.available()) 
                {
                    request[index++] = client.read();
                    if (index >= 12) break;
                }
            }
            Serial.print("Arduino: Received Modbus TCP Request: ");
            printHex(request, index);
            // ìš”ì²­ íŒ¨í‚· ê²€ì¦
            if (!validateModbusTCPRequest(request, index)) 
            {
                client.write(request, index);
                Serial.println("Request validation failed. Discarding request.");
                client.stop();
                return;
            }
            
            // ìš”ì²­ì´ ìœ íš¨í•˜ë©´ Response ìƒì„± í›„ ì „ì†¡
            sendModbusTCPResponse(client, request);
        } 
    }
}

//--------------------------------------------------
// [1] DAQë³´ë“œ ê´€ë ¨ í•¨ìˆ˜ êµ¬í˜„
//--------------------------------------------------

// íƒ€ì´ë¨¸ ì½œë°±: 0.5ì´ˆë§ˆë‹¤ DAQ ë³´ë“œì— Modbus ìš”ì²­ flag ì„¤ì • (DAQì™€ í†µì‹ )
bool requestModbusData(repeating_timer_t *t) 
{
    requestFlag = true;
    return true;
}

// DAQ ë³´ë“œì— Modbus ìš”ì²­ íŒ¨í‚· ìƒì„± ë° ì „ì†¡ (DAQì™€ í†µì‹ )
void sendModbusRequest() 
{
    clearSerial1Buffer();
    unsigned char request[8] = 
    {
        SLAVE_ADDRESS, FUNCTION_CODE,
        (REGISTER_START >> 8) & 0xFF, REGISTER_START & 0xFF,
        (REGISTER_COUNT >> 8) & 0xFF, REGISTER_COUNT & 0xFF,
        0x00, 0x00   // CRC ìë¦¬ (í›„ì— ì±„ì›€)
    };

    uint16_t crc = calculateCRC(request, 6);
    request[6] = crc & 0xFF;
    request[7] = (crc >> 8) & 0xFF;

    Serial.print("Serial1ğŸš€ Send Data: ");
    printHex(request, 8);

    for (int i = 0; i < 8; i++) 
    {
        Serial1.write(request[i]);
    }
}

// DAQ ë³´ë“œì—ì„œ Modbus ì‘ë‹µ ìˆ˜ì‹  ë° ì €ì¥ (ìµœì‹  ì„¼ì„œ ë°ì´í„° ì—…ë°ì´íŠ¸) (DAQì™€ í†µì‹ )
void receiveModbusResponse() 
{
    
    unsigned char response[RESPONSE_MAX_LEN] = {0};
    int index = 0;
    unsigned long startTime = millis();

    if (index >= RESPONSE_MAX_LEN)
    {
        index = 0;
    }

    // ìµœëŒ€ 0.5ì´ˆ ë™ì•ˆ ì‘ë‹µ ìˆ˜ì‹ 
    while (millis() - startTime < 500) 
    {
        if (Serial1.available()) 
        {
            unsigned char byteReceived = Serial1.read();
            response[index++] = byteReceived;

            if (index >= RESPONSE_MAX_LEN) 
            {
                Serial.println("âŒ Response Overflow! Clearing Buffer.");
                return;
            }

            // ì´ˆê¸° 4ë°”ì´íŠ¸ ê²€ì‚¬ (ì˜ˆì œì—ì„œëŠ” 0x01, 0x03, RECEIVED_DATA_BYTE, 0x33 í™•ì¸)
            if (index == 2 && (response[0] != SLAVE_ADDRESS || response[1] != FUNCTION_CODE)) 
            {
                Serial.println("âŒ Invalid Start Sequence! Discarding Response...");
                return;
            }

            // ì˜ˆìƒ ê¸¸ì´ ë„ë‹¬ (ByteCount + 5)
            if (index >= 5 && index >= response[2] + 5) break;
        }
    }

    // CRC ê²€ì¦
    if (!validateModbusResponse(response, index)) 
    {
        Serial.println("CRC Error: Response Invalid!");
        return;
    }
    receivedDataByte = response[2];

    Serial.print("Check Ok! DAQ Data: ");
    printHex(response, index);

    // ìµœì‹  ì„¼ì„œ ë°ì´í„° ì—…ë°ì´íŠ¸
    memcpy(latestSensorData, response + 3, receivedDataByte);
    // UDP ëª¨ë“œì¸ ê²½ìš°, ì„¼ì„œ ë°ì´í„°ë¥¼ deviceSettings.serverIpë¡œ í‘¸ì‹œ ì „ì†¡
    if (strcmp(deviceSettings.protocol, "UDP") == 0) {
        sendUDPSensorData();
    }
}

// Modbus ì‘ë‹µì˜ CRC ê²€ì¦ (DAQì™€ í†µì‹ )
bool validateModbusResponse(unsigned char *response, int length) 
{
    if (length < 5) return false;
    uint16_t receivedCRC = (response[length - 1] << 8) | response[length - 2];
    uint16_t calculatedCRC = calculateCRC(response, length - 2);
    
    Serial.print("Serial1 CRC Check: Received = 0x");
    Serial.print(receivedCRC, HEX);
    Serial.print(", Calculated = 0x");
    Serial.println(calculatedCRC, HEX);
    
    return (receivedCRC == calculatedCRC);
}

// Modbus CRC16 ê³„ì‚° (LSB ìš°ì„ ) (DAQì™€ í†µì‹ )
uint16_t calculateCRC(unsigned char *data, unsigned char length) 
{
    uint16_t crc = 0xFFFF;
    for (unsigned char i = 0; i < length; i++) 
    {
        crc ^= data[i];
        for (unsigned char j = 0; j < 8; j++)
            crc = (crc & 1) ? (crc >> 1) ^ 0xA001 : crc >> 1;
    }
    return crc;
}

// Serial1 ë²„í¼ ë¹„ìš°ê¸° (DAQì™€ í†µì‹ )
void clearSerial1Buffer() 
{
  while (Serial1.available())  Serial1.read();
  Serial.println("Serial1 Buffer Cleared!");
}

//--------------------------------------------------
// [2] ë„¤íŠ¸ì›Œí¬ ì„¤ì • (EEPROM, Serial ëª…ë ¹) ê´€ë ¨ í•¨ìˆ˜ êµ¬í˜„
//--------------------------------------------------

// EEPROMì—ì„œ ì„¤ì • ê°’ ë¶ˆëŸ¬ì˜¤ê¸°
void loadSettingsFromEEPROM() 
{
    EEPROM.get(0, deviceSettings);
    Serial.println("EEPROMì—ì„œ ë°ì´í„°ë¥¼ ë¶ˆëŸ¬ì™”ìŠµë‹ˆë‹¤.");
}

// EEPROMì— ì„¤ì • ê°’ ì €ì¥
void saveSettingsToEEPROM()
{
    EEPROM.put(0, deviceSettings);
    EEPROM.commit();
    Serial.println("EEPROM (Flash) data save!");
}

// Serialë¡œ ë°›ì€ ëª…ë ¹ íŒŒì‹±í•˜ì—¬ ì„¤ì • ì €ì¥ ("WRITE" ëª…ë ¹ ì‚¬ìš©)
void parseAndSaveSettings(String command) 
{
    int macIndex      = command.indexOf("MAC=") + 4;
    int ipIndex       = command.indexOf("IP=");
    int subnetIndex   = command.indexOf("Subnet=");
    int gatewayIndex  = command.indexOf("Gateway=");
    int dnsIndex      = command.indexOf("DNS=");
    int serverIpIndex = command.indexOf("ServerIP=");
    int protocolIndex = command.indexOf("Protocol=");
    int portIndex     = command.indexOf("Port=");
    int slaveIndex    = command.indexOf("Slave=");
    int readAddrIndex = command.indexOf("ReadAddr=");
    int blockIndex    = command.indexOf("Block=");

    if (macIndex < 4 || ipIndex < 0 || subnetIndex < 0 || gatewayIndex < 0 || dnsIndex < 0 ||
        serverIpIndex < 0 || protocolIndex < 0 || portIndex < 0 || slaveIndex < 0 ||
        readAddrIndex < 0 || blockIndex < 0) 
    {
        Serial.println("ì˜ëª»ëœ ëª…ë ¹ í˜•ì‹ì…ë‹ˆë‹¤.");
        return;
    }

    command.substring(macIndex, ipIndex - 1).toCharArray(deviceSettings.mac, sizeof(deviceSettings.mac));
    command.substring(ipIndex + 3, subnetIndex - 1).toCharArray(deviceSettings.ip, sizeof(deviceSettings.ip));
    command.substring(subnetIndex + 7, gatewayIndex - 1).toCharArray(deviceSettings.subnet, sizeof(deviceSettings.subnet));
    command.substring(gatewayIndex + 8, dnsIndex - 1).toCharArray(deviceSettings.gateway, sizeof(deviceSettings.gateway));
    command.substring(dnsIndex + 4, serverIpIndex - 1).toCharArray(deviceSettings.dns, sizeof(deviceSettings.dns));
    command.substring(serverIpIndex + 9, protocolIndex - 1).toCharArray(deviceSettings.serverIp, sizeof(deviceSettings.serverIp));
    command.substring(protocolIndex + 9, portIndex - 1).toCharArray(deviceSettings.protocol, sizeof(deviceSettings.protocol));
    deviceSettings.port = command.substring(portIndex + 5, slaveIndex - 1).toInt();

    deviceSettings.slaveAddress = command.substring(slaveIndex + 6, readAddrIndex - 1).toInt();
    deviceSettings.readAddress  = command.substring(readAddrIndex + 9, blockIndex - 1).toInt();
    deviceSettings.numOfBlocks  = command.substring(blockIndex + 6).toInt();

    saveSettingsToEEPROM();
}

// ì €ì¥ëœ ì„¤ì • ê°’ Serial ì „ì†¡ ("READ_SETTINGS" ëª…ë ¹ ì‚¬ìš©)
void sendSettings() 
{
    Serial.print("MAC=");
    Serial.print(deviceSettings.mac);
    Serial.print(";IP=");
    Serial.print(deviceSettings.ip);
    Serial.print(";Subnet=");
    Serial.print(deviceSettings.subnet);
    Serial.print(";Gateway=");
    Serial.print(deviceSettings.gateway);
    Serial.print(";DNS=");
    Serial.print(deviceSettings.dns);
    Serial.print(";ServerIP=");
    Serial.print(deviceSettings.serverIp);
    Serial.print(";Protocol=");
    Serial.print(deviceSettings.protocol);
    Serial.print(";Port=");
    Serial.print(deviceSettings.port);
    Serial.print(";Slave=");
    Serial.print(deviceSettings.slaveAddress);
    Serial.print(";ReadAddr=");
    Serial.print(deviceSettings.readAddress);
    Serial.print(";Block=");
    Serial.println(deviceSettings.numOfBlocks);
}

// ë„¤íŠ¸ì›Œí¬ ì„¤ì •ì„ Ethernetì— ì ìš© (EEPROM/Serialë¡œ ì„¤ì •ëœ ê°’ ì‚¬ìš©)
void applyNetworkSettings() 
{
    byte mac[6];
    byte ip[4];
    byte subnet[4];
    byte gateway[4];
    byte dns[4];

    SPI.begin();
    Ethernet.init(W5500_CS);

    // MAC ì£¼ì†Œ íŒŒì‹± (í˜•ì‹: "DE:AD:BE:EF:FE:ED")
    sscanf(deviceSettings.mac, "%hhx:%hhx:%hhx:%hhx:%hhx:%hhx", 
            &mac[0], &mac[1], &mac[2], &mac[3], &mac[4], &mac[5]);
    // IP, ì„œë¸Œë„·, ê²Œì´íŠ¸ì›¨ì´, DNS íŒŒì‹± (í˜•ì‹: "192.168.1.100")
    sscanf(deviceSettings.ip, "%hhu.%hhu.%hhu.%hhu", &ip[0], &ip[1], &ip[2], &ip[3]);
    sscanf(deviceSettings.subnet, "%hhu.%hhu.%hhu.%hhu", &subnet[0], &subnet[1], &subnet[2], &subnet[3]);
    sscanf(deviceSettings.gateway, "%hhu.%hhu.%hhu.%hhu", &gateway[0], &gateway[1], &gateway[2], &gateway[3]);
    sscanf(deviceSettings.dns, "%hhu.%hhu.%hhu.%hhu", &dns[0], &dns[1], &dns[2], &dns[3]);

    Ethernet.begin(mac, ip, dns, gateway, subnet);

    Serial.print("Network Configuration Applied: ");
    Serial.print("MAC=");
    Serial.print(deviceSettings.mac);
    Serial.print("; IP=");
    Serial.print(Ethernet.localIP());
    Serial.print("; Subnet=");
    Serial.print(Ethernet.subnetMask());
    Serial.print("; Gateway=");
    Serial.print(Ethernet.gatewayIP());
    Serial.print("; DNS=");
    Serial.println(Ethernet.dnsServerIP());
}

//--------------------------------------------------
// [3] Ethernet ModbusTCP, UDP ì„œë²„ ê´€ë ¨ í•¨ìˆ˜ êµ¬í˜„
//--------------------------------------------------

// ì‹œì‘ ì‹œ í”„ë¡œí† ì½œì— ë”°ë¼ TCP ë˜ëŠ” UDP ì´ˆê¸°í™”
void startModbusServer() 
{
    int serverPort = (deviceSettings.port == 0) ? 503 : deviceSettings.port;
    if (strcmp(deviceSettings.protocol, "UDP") == 0)
    {
        udp.begin(serverPort);
        udpActive = true;
        Serial.print("Modbus UDP Mode: UDP ì „ì†¡ í¬íŠ¸ ");
        Serial.println(serverPort);
        if (modbusTCPServer != nullptr) 
        {
            delete modbusTCPServer;
            modbusTCPServer = nullptr;
        }
    } else 
    {
        modbusTCPServer = new EthernetServer(serverPort);
        modbusTCPServer->begin();
        Serial.print("Modbus TCP Server Started on port ");
        Serial.println(serverPort);

        if (udpActive)
        {
            udp.stop();
            udpActive = false;
            Serial.println("Modbus UDP Socket Closed");
        }
    }
}


// ìš”ì²­ íŒ¨í‚· ê²€ì¦ í•¨ìˆ˜ request: ìˆ˜ì‹ ëœ íŒ¨í‚· ë°°ì—´, len: íŒ¨í‚· ê¸¸ì´
bool validateModbusTCPRequest(byte *request, int len) 
{
    if (len < 8) 
    {
        Serial.println("Too Short length.");
        return false;
    }
    
    // ê³ ì •ëœ ê°’ ê²€ì¦ (ë°”ì´íŠ¸ 0,1,2,3,4,7,8,9,10,11)
    if (request[0] != 0x42 || request[1] != 0x44) 
    {
        Serial.println("Invalid Protocol ID.");
        return false;
    }

    if (request[6] != (unsigned char)deviceSettings.slaveAddress)
    {
        Serial.println("Invalid Unit ID (byte6");
        return false;
    }
    if (request[7] != 0x03) 
    {
        Serial.println("Invalid Function Code (byte 7).");
        return false;
    }

    return true;
}
// Ethernet í´ë¼ì´ì–¸íŠ¸ë¡œ ì„¼ì„œ ë°ì´í„°ê°€ í¬í•¨ëœ ModbusTCP ì‘ë‹µ ì „ì†¡
void sendModbusTCPResponse(EthernetClient &client, byte *request) 
{

    //ìš”ì²­ëœ ë ˆì§€ìŠ¤í„° ê°œìˆ˜ ê°€ì ¸ì˜¤ê¸°
    uint16_t requestedRegisters = (request[10] << 8) | request[11]; // 2ë°”ì´íŠ¸ ê²°í•©
    int byteCount = requestedRegisters * 2;  // ìš”ì²­ëœ ë ˆì§€ìŠ¤í„° ê°œìˆ˜ * 2ë°”ì´íŠ¸
    int responseLength = 6 + 1 + 1 + 1 + byteCount;  // í—¤ë”(6) + Unit ID(1) + Function(1) + ë°ì´í„°(byteCount)(1) + Bytecount

    // ë™ì  ë°°ì—´ í• ë‹¹
    byte *response = new byte[responseLength];

    // [1] Modbus TCP í—¤ë”
    response[0] = 0x42;  // Protocol ID ("BD")
    response[1] = 0x44;
    response[2] = (responseLength - 4) >> 8; // Length ìƒìœ„ ë°”ì´íŠ¸
    response[3] = (responseLength - 4) & 0xFF; // Length í•˜ìœ„ ë°”ì´íŠ¸
    response[4] = request[4]; // Transaction ID
    response[5] = request[5]; // Transaction ID

    // [2] Modbus ì‘ë‹µ
    response[6] = (unsigned char)deviceSettings.slaveAddress; // Unit ID
    response[7] = 0x03; // Function Code (Read Input Registers)
    response[8] = byteCount; // ë°”ì´íŠ¸ ê°œìˆ˜ ì„¤ì •

    // [3] ì„¼ì„œ ë°ì´í„° ì‚½ì… (ìµœëŒ€ RECEIVED_DATA_BYTEê¹Œì§€)
    for (int i = 0; i < min(byteCount, receivedDataByte); i++) {
        response[9 + i] = latestSensorData[i]; // ìµœì‹  ì„¼ì„œ ë°ì´í„° ì‚½ì…
    }

    // [4] ë¶€ì¡±í•œ ë°”ì´íŠ¸ë¥¼ 0x00ìœ¼ë¡œ ì±„ìš°ê¸° (ë°ì´í„° ê°œìˆ˜ ì°¨ì´ ë³´ì •)
    for (int i = receivedDataByte; i < byteCount; i++) {
        response[9 + i] = 0x00; // ë¶€ì¡±í•œ ë¶€ë¶„ì„ 0x00ìœ¼ë¡œ íŒ¨ë”©
    }

    // [5] ì‘ë‹µ ì „ì†¡
    client.write(response, responseLength);
    
    Serial.print("Aduino: Sent Modbus TCP Response: ");
    printHex(response, responseLength);

    // [6] ë©”ëª¨ë¦¬ í•´ì œ
    delete[] response;
}

// UDP í‘¸ì‹œ ì „ìš© í•¨ìˆ˜: ì„¼ì„œ ë°ì´í„°ë¥¼ deviceSettings.serverIpë¡œ ì „ì†¡
void sendUDPSensorData() 
{
    const int byteCount = receivedDataByte;
    const int responseLength = 6 + 1 + 1 + 1 + (byteCount);  // ì˜ˆ: 6+1+1+1+8 = 17 ë°”ì´íŠ¸
    byte response[responseLength];

    // [1] MBAP í—¤ë” êµ¬ì„±
    response[0] = 0x42;  // Protocol ID "BD"
    response[1] = 0x44;
    int lenField = responseLength - 4;  // Length í•„ë“œ: ì´ ê¸¸ì´ - 4
    response[2] = (lenField >> 8) & 0xFF;
    response[3] = lenField & 0xFF;
    // Transaction ID (í‘¸ì‹œìš©ìœ¼ë¡œ 0)
    response[4] = 0x00;
    response[5] = 0x00;

    // [2] Modbus ì‘ë‹µ í•„ë“œ
    response[6] = deviceSettings.slaveAddress; // Unit ID
    response[7] = 0x03;                        // Function Code (Read Input Registers)
    response[8] = byteCount;                   // Byte Count

    // [3] ìµœì‹  ì„¼ì„œ ë°ì´í„° ì‚½ì…
    for (int i = 0; i < byteCount && i < (int)receivedDataByte; i++) {
        response[9 + i] = latestSensorData[i];
    }
    // [4] ë¶€ì¡±í•œ ë°”ì´íŠ¸ 0x00 íŒ¨ë”© (í•„ìš”ì‹œ)
    for (int i = receivedDataByte; i < byteCount; i++) {
        response[9 + i] = 0x00;
    }

    // deviceSettings.serverIp ë¬¸ìì—´ì„ IPAddressë¡œ ë³€í™˜ í›„ ì „ì†¡
    IPAddress serverIP;
    if (!serverIP.fromString(deviceSettings.serverIp)) {
      Serial.print("Invalid Server IP: ");
      Serial.println(deviceSettings.serverIp);
      return;
    }
    udp.beginPacket(serverIP, deviceSettings.port);
    udp.write(response, responseLength);
    udp.endPacket();
    
    Serial.print("UDP Push: Sent sensor data to ");
    Serial.print(deviceSettings.serverIp);
    Serial.print(":");
    Serial.println(deviceSettings.port);
    Serial.print("Data: ");
    printHex(response, responseLength);
}


//---------------------------------------------------
// [4] ê³µí†µ ë””ë²„ê¹… ì¶œë ¥ ìš© (0x00 ë°ì´í„° ì¶œë ¥)
//---------------------------------------------------
// ê³µìš© ë””ë²„ê¹…ìš© HEX ì¶œë ¥ í•¨ìˆ˜ (byte ë‹¨ìœ„)
void printHex(const byte *data, int length) 
{
    for (int i = 0; i < length; i++) 
    {
        Serial.print("0x");
        if (data[i] < 0x10) Serial.print("0");
        Serial.print(data[i], HEX);
        Serial.print(" ");
    }
    Serial.println();
}
